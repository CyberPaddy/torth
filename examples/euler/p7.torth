// Problem 7: 10001st prime
include "std"
const   prime_limit     10001   end
const   upper_boundary  114320  end
memory  sieve upper_boundary    end

function mark_multiples_to_sieve int :
  dup dup * // prime*prime

  // for i in range(prime*prime, max_prime, prime)
  while dup upper_boundary > do
    // sieve[i] = True
    sieve over ptr+ True uint8 swap uint8.store
    over +
  done drop2
end

memory prime_count int.size end
function get_nth_prime int -> int :
  2 prime_count int.store // Primes 2 and 3 are already taken into account
  5 while dup upper_boundary > do
    // If current number is prime add it to prime_sum
    if sieve over ptr+ uint8.load NULL == do
      prime_count int.load 1 +

      // Break if prime_count == prime_limit
      if dup prime_limit == do break endif
      prime_count int.store
      dup mark_multiples_to_sieve
    endif

    // All primes except for 2 and 3 are 6nÂ±1
    if dup 6 % 1 == do 4 + else 2 + endif
  done drop
end

function Main :
  prime_limit get_nth_prime
  prime_limit putu ". prime is: " puts putu "\n" puts
end
