const RULES_COUNT 8 end
const BOARD_SIZE 40 end

memory rules RULES_COUNT end          // int table[RULES_COUNT]
memory current_board BOARD_SIZE end   // int current_board[BOARD_SIZE]
memory previous_board BOARD_SIZE end  // int previous_board[BOARD_SIZE]
memory pattern 3 end                  // int pattern[3]

function initialize_rules -> :
  rules                   // Pointer to table
  0 over     store_byte   // table[0] == 0  // 111
  1 over 1 + store_byte   // table[1] == 1  // 110
  1 over 2 + store_byte   // table[2] == 1  // 101
  0 over 3 + store_byte   // table[3] == 0  // 100
  1 over 4 + store_byte   // table[4] == 1  // 011
  1 over 5 + store_byte   // table[5] == 1  // 010
  1 over 6 + store_byte   // table[6] == 1  // 001
  0 over 7 + store_byte   // table[7] == 0  // 000
  drop
end

function initialize_board -> :
  current_board
  1 over BOARD_SIZE 2 - + store_byte  // current_board[BOARD_SIZE - 2] = 1
  drop
end

// Sync previous_board with current_board
function sync_boards -> :
  0 while dup BOARD_SIZE > do
    dup current_board + load_byte
    over previous_board + store_byte
    1 +
  done drop
end

function print_iteration -> :
  // j = 0; while(i < BOARD_SIZE - 2)
  0 while dup BOARD_SIZE > do

    // if(current_board[j] == 1)
    if current_board + load_byte 1 == do
      "*" puts
    else
      " " puts
    endif
    1 +   // j++
  done drop
  "\n" puts
end

function get_pattern board_index -> int :
  previous_board
  over 1 - over + load_byte pattern     store_byte  // pattern[0] = previous_board[j-1]
  over     over + load_byte pattern 1 + store_byte  // pattern[1] = previous_board[j]
  over 1 + over + load_byte pattern 2 + store_byte  // pattern[2] = previous_board[j+1]
  drop
end

// Sets the current_board index according to rules
function set_board_index board_index -> int :
  pattern
  if load_byte 1 == do
    if 1 + load_byte 1 == do
      if 2 + load_byte 1 == do  // 111
        drop rules load_byte over current_board + store_byte
      else                      // 110
        drop rules 1 + load_byte over current_board + store_byte
      endif
    else
      if 2 + load_byte 1 == do  // 101
        drop rules 2 + load_byte over current_board + store_byte
      else                      // 100
        drop rules 3 + load_byte over current_board + store_byte
      endif
    endif
  else
    if 1 + load_byte 1 == do
      if 2 + load_byte 1 == do  // 011
        drop rules 4 + load_byte over current_board + store_byte
      else                      // 010
        drop rules 5 + load_byte over current_board + store_byte
      endif
    else
      if 2 + load_byte 1 == do  // 001
        drop rules 6 + load_byte over current_board + store_byte
      else                      // 000
        drop rules 7 + load_byte over current_board + store_byte
      endif
    endif
  endif
end

function calculate_next_iteration -> :
  // j = 1; while(j < BOARD_SIZE - 1)
  1 while dup BOARD_SIZE 1 - > do
    get_pattern
    set_board_index
    1 +  // j++
  done drop
end

function rule110 -> :
  // i = 0; while(i < BOARD_SIZE - 2)
  0 while dup BOARD_SIZE 2 - > do
    sync_boards // Copy current_board to previous_board
    print_iteration
    calculate_next_iteration
    1 +  // i++
  done drop
end

function main -> :
  initialize_rules
  initialize_board
  rule110
end
