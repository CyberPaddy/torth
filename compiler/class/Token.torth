// @dataclass
// class Token:
//     value: str
//     type: TokenType
//     location: Location
const Token.size 40 end
function Token.value  ptr -> str        :          load_STR end
function Token.type   ptr -> TokenType  : 8   ptr+ load_INT end
function Token.file   ptr -> str        : 16  ptr+ load_STR end
function Token.row    ptr -> int        : 24  ptr+ load_INT end
function Token.col    ptr -> int        : 32  ptr+ load_INT end

// Pointer to the list of Tokens
memory tokens MEMORY_CAPACITY end

// *Token, value, TokenType, file, row, col
function Token.store ptr str int str int int -> :
  dup rot swap store_STR ptr++   // Token.value
  dup rot swap store_INT ptr++   // Token.type
  dup rot swap store_STR ptr++   // Token.file
  dup rot swap store_INT ptr++   // Token.row
  dup rot swap store_INT ptr++   // Token.col
  drop
end

function Token.getType ptr -> str :
  Token.type
  if dup TokenType.ARRAY == do
    "TokenType.ARRAY"
  elif dup TokenType.BOOL == do
    "TokenType.BOOL"
  elif dup TokenType.CHAR == do
    "TokenType.CHAR"
  elif dup TokenType.HEX == do
    "TokenType.HEX"
  elif dup TokenType.INT == do
    "TokenType.INT"
  elif dup TokenType.KEYWORD == do
    "TokenType.KEYWORD"
  elif dup TokenType.PTR == do
    "TokenType.PTR"
  elif dup TokenType.STR == do
    "TokenType.STR"
  elif dup TokenType.WORD == do
    "TokenType.WORD"
  else
    "ERROR: TokenType " puts dup print " is not defined.\n" puts 1 exit ""
  endif
  swap drop // Drop TokenType
end

function Token.output ptr -> :
  dup "Type     => " puts Token.getType puts "\n" puts
  dup "Location => File: " puts Token.file puts
  dup ", Row: " puts Token.row print
  dup ", Column: " puts Token.col print "\n" puts
  drop
end

function Token.puts ptr -> :
  dup "Token    => " puts load_STR puts "\n" puts
  Token.output
end
